---
eip: 5564
title: Non-Interactive Stealth Address Generation
description: Stealth addresses for private transfers
author: Anton Wahrstätter (@nerolation), Matt Solomon (@mds1), Ben DiFrancesco (@apbendi), Vitalik Buterin <vitalik.buterin@ethereum.org>
discussions-to: https://ethereum-magicians.org/t/eip-5566-stealth-addresses-for-smart-contract-wallets/10614
status: Draft
type: Standards Track
category: ERC
created: 2022-08-13
---


## Abstract
This specification defines a standardized way of creating stealth addresses. This EIP enables senders of transactions/transfers to non-interactively generate private stealth addresses for their recipients that only the recipients can unlock.

## Motivation
The standardization of non-interactive stealth address generation holds the potential to greatly enhance the privacy capabilities of Ethereum by enabling the recipient of a transfer to remain anonymous when receiving an asset. This is achieved through the generation of a stealth address by the sender, using a shared secret between the sender and recipient. Only the recipient is able to unlock the funds at the stealth address, as they are the only ones with access to the private key required for this purpose. As a result, observers are unable to link the recipient's stealth address to their identity, preserving the privacy of the recipient and leaving only the sender with this information.

## Specification
The key words “MUST”, “MUST NOT”, “REQUIRED”, “SHALL”, “SHALL NOT”, “SHOULD”, “SHOULD NOT”, “RECOMMENDED”, “MAY”, and “OPTIONAL” in this document are to be interpreted as described in RFC 2119.

The follow contracts are part of this specification:
- `IERC5564Registry` stores the stealth public keys for users. This MUST be a singleton contract, with one instance per chain.
- `IERC5565Generator` contracts are used to compute stealth addresses for a user based on a given curve. There can be many of these per chain, and for a given curve there SHOULD be one implementation per chain.
- `IERC5564Messenger` emits events to announce when something is sent to a stealth address. This MUST be a singleton contract, with one instance per chain.

The interface for each is specified as follows:

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.0;

/// @notice Registry mapping an address to its stealth key information.
interface IERC5564Registry {
  /// @notice Returns the stealth public keys for the given `registrant` to compute a stealth
  /// address accessible only to that `registrant` using the provided `generator` contract.
  /// @dev MUST return zero if a registrant has not registered keys for the given generator.
  /// @dev The pubkeys are returned as `bytes memory` to support pubkeys of any  format and size.
  /// @dev Generator contracts SHOULD be written to support a single public key, or two public keys:
  /// one viewing key and one spending key. This can be inferred from the length of the key material.
  function stealthKeys(address registrant, address generator)
    external
    view
    returns (bytes memory pubKey);

  /// @notice Sets the caller's stealth public keys for the `generator` contract.
  /// @dev The pubkey is passed as `bytes memory` to support pubkeys of any format and size.
  /// @dev Generator contracts SHOULD be written to support a single public key, or two public keys:
  /// one viewing key and one spending key. This can be inferred from the length of the key material.
  function registerKeys(address generator, bytes memory pubKey) external;

  /// @notice Sets the `registrant`s stealth public keys for the `generator` contract using their
  /// signature, given by `v`, `r`, and `s`.
  /// @dev The pubkey is passed as `bytes memory` to support pubkeys of any format and size.
  /// @dev Generator contracts SHOULD be written to support a single public key, or two public keys:
  /// one viewing key and one spending key. This can be inferred from the length of the key material.
  function registerKeysOnBehalf(
    address registrant,
    uint8 v,
    bytes32 r,
    bytes32 s,
    address generator,
    bytes memory pubKey
  ) external;

  /// @dev Event emitted when a registrant updates their registered stealth keys.
  event StealthKeyChanged(address indexed registrant, address indexed generator, bytes pubKey);
}

/// @notice Interface for generating stealth addresses for keys on a given curve.
/// @dev The Generator contract SHOULD have a method called decodeKeys that returns the recipient's
/// public keys as the correct types. This is not included below since the return parameters may
/// differ for each Generator.
interface IERC5564Generator {
  /// @notice Given a `registrant`, returns all relevant data to compute a stealth address.
  /// @dev MUST return all zeroes if the registrant has not registered keys for this generator.
  /// @dev Intended to be used off-chain only to prevent exposing secrets on-chain.
  /// @dev Consider running this against a local node, or using an off-chain library with the same
  /// logic, instead of via an `eth_call` to a public RPC provider to avoid leaking secrets.
  /// @dev Consult the generator's documentation for details on how to format this parameter.
  function generateStealthAddress(address registrant)
    external
    view
    returns (
      address stealthAddress,
      uint256 sharedSecret,
      bytes32 sharedSecretHash,
      bytes32 viewTag,
      bytes memory ephemeralPubKey
    );
}

/// @notice Interface for sending tokens to a stealth address.
interface IERC5564Messenger {
  /// @dev Emitted when sending something to a stealth address.
  /// @dev ephemeralPubKey represents the ephemeral public key of the sender.
  /// @dev StealthRecipientandViewTag contains the stealth address (20 bytes) and the view tag (12 bytes).
  /// @dev Metadata is an arbitrary field that the sender can use however they like, but the below
  /// guidelines are recommended:
  ///   - When sending ERC-20 tokens, the metadata SHOULD include the token address as the first 20
  ///     bytes, and the amount being sent as the following 32 bytes.
  ///   - When sending ERC-721 tokens, the metadata SHOULD include the token address as the first 20
  ///     bytes, and the token ID being sent as the following 32 bytes.
  event Announcement(
    bytes ephemeralPubKey,
    bytes32 indexed stealthRecipientandViewTag,
    bytes32 metadata
  );
}

/// @notice Sample implementation for generating stealth addresses for keys on the secp256k1 curve.
function generateStealthAddress(address registrant, bytes memory ephemeralPrivKey)
  external
  view
  returns (
    address stealthAddress,
    uint256 sharedSecret,
    bytes32 sharedSecretHash,
    bytes memory ephemeralPubKey,
    bytes memory viewTag
  )
{
  // Get user's public keys.
  (bytes memory viewPubKey, bytes memory spendPubKey) =
    IERC5564Registry(registry).stealthKeys(registrant, address(this));

  // Generate shared secret from sender's private key and recipient's viewing key.
  sharedSecret = ecMul(ephemeralPrivKey, viewPubKey);
  sharedSecretHash = keccak256(sharedSecret);
  
  // Generate view tag for enabling faster parsing for the recipient
  viewTag = sharedSecretHash[0:12];

  // Generate a point from the hash of the shared secret
  sharedSecretPoint = ecMul(sharedSecretHash, G);

  // Generate sender's public key from their ephemeral private key.
  stealthPubKey = ecAdd(spendPubKey, sharedSecretPoint);

  // Compute stealth address from the steath public key.
  stealthAddress = pubkeyToAddress(ephemeralPubKey);
}
```

Stealth addresses are computed using the algorithm below, assuming elliptic curves.
Other encryption schemes such as post-quantum encryption with Kyber may need to modify this approach.
- $G$ is the generator point of the curve.
- Recipient has private keys $p_{view}$ and $p_{spend}$.
- Recipient publishes corresponding public keys $P_{view}$ and $P_{spend}$ in the `IERC5564Registry`.
- Sender generates random 32-byte entropy ephemeral private key $p_{ephemeral}$.
- Sender passes the recipient address and $p_{ephemeral}$ to the `IERC5564Generator` contract's `generateStealthAddress` function.
- This function performs the following computations:
  - A shared secret $s$ is computed as $s = p_{ephemeral} \cdot P_{view}$.
  - The secret is hashed $s_{h} = h(s)$.
  - The viewTag is extracted by taking the most significant 12 bytes $s_{h}[0:12]$, 
  - Multiplying the hashed secret with the generator point $S = s_{h} \cdot G$.
  - The recipient's stealth public key is computed as $P_{stealth} = P_{spend} + S$.
  - The recipient's stealth address is computed as $pubkeyToAddress(P_{stealth})$.

Sending funds now works as follows:
- Sender uses the contract of their choice to send something to $a_{stealth}$, and provides $P_{ephemeral}$ and any other metadata to the send method.
- The contract calls `IERC5564Messenger.announce` with $a_{stealth} + viewTag$, $P_{ephemeral}$, and any metadata.

To scan for funds, a recipient must retrieve all logs from the `IERC5564Messenger` contract.
They then check if they can compute the stealth address $P_{stealth}$ that was emitted as stealth address $a_{stealth}$in the `Announcement`. If successful, the recipient can generate  $p_{stealth}$, representing the private key that can eventually access $P_{stealth}$.

The parsing process can be presented as follows:
- Recipient has private keys $p_{view}$ and $p_{spend}$.
- Recipient parses all Announcements $AN$ performs the following operations:
- This function performs the following computations:
  - Computing the shared secret $s$ is computed as $s = AN \cdot p_{view}$.
  - Hashing the secret, $s_{h} = h(s)$.
  - Comparing the most significant 12 bytes of the resulting hash with the view tag emitted in the event and continue if matching
  - Multiplying the hashed secret with the generator point $S = s_{h} \cdot G$.
  - Compute stealth public key  as $P_{stealth} = P_{spend} + S$.
  - The recipient's address is computed as $stealthAddress = pubkeyToAddress(P_{stealth})$.
  - Compare $stealthAddress$ with the stealth address logged the emitted event.

### Parsing considerations
Usually, the recipient of a stealth address transaction has to perform the following operations to check weather he was the recipient of a certain transaction:
- 2x ecMUL,
- 2x HASH,
- 1x ecADD,

The [view tags](https://github.com/monero-project/research-lab/issues/73) approach was introduced in order to reduce the parsing time by around 6x. Users only need to perform 1x ecMUL and 1x HASH (skipping 1x ecMUL, 1x ecADD and 1x HASH) for every parsed announcement. The 12 bytes length was is based on the freely available space in the first log of the `Announcement` Event. With 12 bytes as `viewTag` the probability for users to skip the remaining computations after hashing the shared secret $h(s)$ can be determined as follows: $1/(256^{12})$. This means that users can almost certainly skip the above three operations for any announcents that to do not involve them.

## Rationale
This EIP emerged from the need of having privacy-preserving ways to transfer ownership without revealing the recipient's identity. Tokens can reveal sensitive private information about the owner. While users might want to donate money to a specific organisation/country but they might not want to reveal personal account-related information at the same time. The standardization of stealth address generation represents a significant effort for privacy: privacy-preserving solutions require standards to gain adoption, therefore it is critical to focus on generalizable ways of implementing related solutions.

The stealth address extension standardizes a protocol for generating and locating stealth addresses, enabling the transfer of assets without the need for prior interaction with the recipient and allowing recipients to verify the receipt of a transfer without interacting with the blockchain. Importantly, stealth addresses allow the recipient of a token transfer to verify receipt while maintaining their privacy, as only the recipient is able to see that they have been the recipient of the transfer.

The authors identify the trade-off between on- and off-chain efficiency: Although, including a Monero-like `view tags` mechanism helps recipients to parse announcements more quickly, it adds complexity to the announcement event.

The address of the recipient and the `viewTag` MUST be included in the announcement event, allowing users to quickly verify ownership without having to query the chain for positive account balances.

## Backwards Compatibility
This EIP is fully backward compatible.

## Reference Implementation
You can find an implementation of this standard in [eip5564.sol](../assets/eip-5564/eip5564.sol).

## Security Considerations
The funding of the stealth address wallet represents a known issue that might breach privacy. The wallet that funds the stealth address MUST NOT have any physical connection to the stealth address owner in order to fully leverage the privacy improvements.

## Copyright
Copyright and related rights waived via [CC0](../LICENSE.md).
